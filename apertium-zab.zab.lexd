#!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
#!!                                                                         !!!
#!!            V A L L E Y  Z A P O T E C  T R A N S D U C E R              !!!
#!!                                                                         !!!
#!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

#=============================================================================!
#                         Verb Morphotactics                                  !
#=============================================================================!

PATTERNS

Nouns
Verbs
Punctuation

NP-Stems(1) [<np>:] NP-Stems(3):
NP-Stems(1):NP-Stems(2) [<np>:] NP-Stems(3):     # ! Dir/LR

Prep-Stems(1) [<pr>:]
Prep-Stems(1):Prep-Stems(2) [<pr>:]              # ! Dir/LR

Det-Stems(1) [<det>:] Det-Stems(3):
Det-Stems(1):Det-Stems(2) [<det>:] Det-Stems(3): # ! Dir/LR

Postdet-Stems(1) [<postdet>:] Postdet-Stems(3):
Postdet-Stems(1):Postdet-Stems(2) [<postdet>:] Postdet-Stems(3):  # ! Dir/LR

Adv-Stems(1) [<adv>:]
Adv-Stems(1):Adv-Stems(2) [<adv>:]               # ! Dir/LR

Adv-Stems-Itg(1) [<adv><itg>:]
Adv-Stems-Itg(1):Adv-Stems-Itg(2) [<adv><itg>:]  # ! Dir/LR

Adj-Stems(1) [<adj>:]
Adj-Stems(1):Adj-Stems(2) [<adj>:]               # ! Dir/LR

Ij-Stems(1) [<ij>:]
Ij-Stems(1):Ij-Stems(2) [<ij>:]                  # ! Dir/LR

Mod-Stems(1) [<mod>:]
Mod-Stems(1):Mod-Stems(2) [<mod>:]               # ! Dir/LR

Pronouns(1) [<prn>:] Pronouns(3):
Pronouns(1):Pronouns(2) [<prn>:] Pronouns(3):    # ! Dir/LR

Number-Stems(1) [<num>:]
Number-Stems(1):Number-Stems(2) [<num>:]         # ! Dir/LR

# For light verb constructions, with runy

Aux(1) [<aux>:]
Aux(1):Aux(2) [<aux>:]                           # ! Dir/LR



PATTERN Verbs

:Aspect V-Stems(1) :V-Extender(1) :V-Neg(1) :Prn-Bound(1) [<v>:] V-Stems(3): V-Extender(1): Aspect: V-Neg(1): Prn-Bound(1):

:Aspect V-Stems(1):V-Stems(2) :V-Extender(2) :V-Neg(2) :Prn-Bound(2) [<v>:] V-Stems(3): V-Extender(2): Aspect: V-Neg(2): Prn-Bound(2):  # ! Dir/LR

:Aspect V-Stems-Irreg(1)[p1pl,perf] :V-Extender(1) :V-Neg(1) :Prn-Bound(1)[p1pl,perf] [<v>:] V-Stems-Irreg(3): V-Extender(1): Aspect: V-Neg(1): Prn-Bound(1)[p1pl,perf]:
:Aspect V-Stems-Irreg(1)[-p1pl,perf] :V-Extender(1) :V-Neg(1) :Prn-Bound(1)[-p1pl,perf] [<v>:] V-Stems-Irreg(3): V-Extender(1): Aspect: V-Neg(1): Prn-Bound(1)[-p1pl,perf]:
:Aspect V-Stems-Irreg(1)[p1pl,-perf] :V-Extender(1) :V-Neg(1) :Prn-Bound(1)[p1pl,-perf] [<v>:] V-Stems-Irreg(3): V-Extender(1): Aspect: V-Neg(1): Prn-Bound(1)[p1pl,-perf]:
:Aspect V-Stems-Irreg(1)[-p1pl,-perf] :V-Extender(1) :V-Neg(1) :Prn-Bound(1)[-p1pl,-perf] [<v>:] V-Stems-Irreg(3): V-Extender(1): Aspect: V-Neg(1): Prn-Bound(1)[-p1pl,-perf]:



PATTERN Nouns

# Bare nouns
N-Stems(1) [<n>:]
N-Stems(1):N-Stems(2) [<n>:]    # Dir/LR

# Possessed nouns
:N-Poss(1) N-Stems(1) [<n>:] N-Poss(3): Prn-Bound(1)
:N-Poss(2) N-Stems(1):N-Stems(2) [<n>:] N-Poss(3): Prn-Bound(1)  # Dir/LR

# Inherently possessed nouns
N-Stems-Possessed(1) [<n>:] Prn-Bound(1)
N-Stems-Possessed(1):N-Stems-Possessed(2) [<n>:] Prn-Bound(2)    # Dir/LR


LEXICON Aspect

<hab>:r>                    # habitual
<perf>[perf]:{B}>[perf]     # perfective
<prog>:ca{y}>               # progressive
<irre>:{g}{Y}>              # irrealis

LEXICON N-Poss(3)

# FIXME: deal with irregulars, e.g. xyecw/x꞉yèeʼcw for becw
x     x꞉     <px>    # for adding pronominal possession to Ns FIXME: CHECK(2)


LEXICON Prn-Bound(2)           # FIXME

+a<prn><pers><p1><sg>:>a                 +aʼ<prn><pers><p1><sg>:>aʼ
+ën<prn><pers><p1><pl>[p1pl]:>{u}ën[p1pl]      +ëhnn<prn><pers><p1><pl>[p1pl]:>{u}ëhnn[p1pl]

+u<prn><pers><p2><infm><sg>:>u           +ùuʼ<prn><pers><p2><infm><sg>:>ùuʼ
+yu<prn><pers><p2><frm><sg>:>yu          +yuuʼ<prn><pers><p2><frm><sg>:>yuuʼ
+yëbu<prn><pers><p2><frm><sg>:>yëbu      +yëbuuʼ<prn><pers><p2><frm><sg>:>yëbuuʼ   # ! Dir/LR
+ad<prn><pers><p2><infm><pl>:>ad         +ad<prn><pers><p2><infm><pl>:>ad      # FIXME: CHECK(2)
+yuad<prn><pers><p2><frm><pl>:>yuad      +yùad<prn><pers><p2><frm><sg>:>yùad
+yëbuad<prn><pers><p2><frm><pl>:>yëbuad  +yëbùad<prn><pers><p2><frm><sg>:>yëbùad   # ! Dir/LR

+ëng<prn><pers><p3><prox><sg>:>{u}ëng    +ëng<prn><pers><p3><prox><sg>:>{u}ëng
+i<prn><pers><p3><dist><sg>:>{u}i        +ih<prn><pers><p3><dist><sg>:>{u}ih
+ëb<prn><pers><p3><resp><sg>:>{u}ëb      +ëhb<prn><pers><p3><resp><sg>:>{u}ëhb   # FIXME: CHECK(2)
+ëm<prn><pers><p3><anml><sg>:>{u}ëm      +ëhm<prn><pers><p3><anml><sg>:>{u}ëhm   # FIXME: CHECK(2)
+azh<prn><pers><p3><fam><sg>:>azh        +azh<prn><pers><p3><fam><sg>:>azh       # FIXME: CHECK(2)
+iny<prn><pers><p3><rev><sg>:>{u}iny     +iny<prn><pers><p3><rev><sg>:>{u}iny    # FIXME: CHECK(2), (1): iny/ni

+rëng<prn><pers><p3><prox><pl>:>rëng     +rëng<prn><pers><p3><prox><pl>:>rëng    # FIXME: CHECK(2)
+ri<prn><pers><p3><dist><pl>:>ri         +rih<prn><pers><p3><dist><pl>:>rih     # FIXME: CHECK(2)
+rëb<prn><pers><p3><resp><pl>:>rëb       +rëhb<prn><pers><p3><resp><pl>:>rëhb    # FIXME: CHECK(2)
+rëm<prn><pers><p3><anml><pl>:>rëm       +rëhm<prn><pers><p3><anml><pl>:>rëhm    # FIXME: CHECK(2)
+razh<prn><pers><p3><fam><pl>:>razh      +razh<prn><pers><p3><fam><pl>:>razh     # FIXME: CHECK(2)
+riny<prn><pers><p3><rev><pl>:>riny      +riny<prn><pers><p3><rev><pl>:>riny     # FIXME: CHECK(2)

:                           :

LEXICON Pronouns(3)        # Free pronouns

tu           tu               <itg>   # "who"
xi           xi               <itg>   # "what"

danoën:danoën dannooh-ëhnn:dannooh-ëhnn  <pers><p1><pl> # "we"
danoën:danuën dannooh-ëhnn:dannuuh-ëhnn  <pers><p1><pl> # "we"  ! Dir/LR

liu          lìuʼ             <pers><p2><infm><sg>  # "you (sg, informal)"
laad         làaʼ-ahd         <pers><p2><infm><pl>  # "you (pl, informal)"
yu           yuʼuuʼ           <pers><p2><frm><sg>   # "you (sg, formal)"
yu:yëbu      yuʼuuʼ:yəbuʼuuʼ  <pers><p2><frm><sg>   # "you (sg, formal)" ! Dir/LR
yuad         yùad             <pers><p2><frm><pl>   # "you (pl, formal)"
yuad:yëbuad  yuad:yëbùad      <pers><p2><frm><pl>   # "you (pl, formal)" ! Dir/LR

lai          laʼàiʼ           <pers><p3><dist><sg>  # "he/she/it (distal)"
laëng        laʼa-ëng         <pers><p3><prox><sg>  # "he/she/it (proximal)"
laëb         làaʼ-ëhb         <pers><p3><resp><sg>  # "he/she/it (respectful)"
laëm         làaʼ-ëhmm        <pers><p3><anml><sg>  # "he/she/it (animal)"

lari         làaʼri           <pers><p3><dist><pl>  # "they/them (distal)"
larëng       làaʼrëng         <pers><p3><prox><pl>  # "they/them (proximal)"
larëb        làaʼrëhb         <pers><p3><resp><pl>  # "they/them (respectful)"
larëm        làaʼrëhmm        <pers><p3><anml><sg>  # "they/them (animal)"


LEXICON V-Neg(2)            # FIXME        

<neg>:>d{I}                 <neg>:>d{I}ʼ
:                           :

LEXICON V-Extender(2)       # FIXME
<pol>:>la                   <pol>:>làaʼ
:                           :

#LEXICON Caus
#
#<caus>:su
#:


LEXICON Aux(2)

alabar         alabaar          # "praise"
arreglad       arreglaad        # "arrange for"
bda            bdaa             # "cat a shadow, give shade"
bgw            bgwah            # "tie a knot"
bisy           biʼsy            # "let (someone) know"
cas            caʼs             # "pay attention to"
castiu         castiʼu          # "punish"
col            coʼoll           # "stand in line"
company        compaʼanny       # "accompany"
comprender     comprendeer      # "understand"
contest        connteʼst        # "answer"
dany           daany            # "make (someone) sick; damage"
defender       defendeer        # "defend"
fald           faʼlld           # "do something wrong somewhere; miss / be absent from somewhere"
gan            gaan             # "earn, win"
gaxt           gaʼaxt           # "spend money"
guan           guuhahnn         # "make charitable donation"
mandad         mandaad          # "run an errand; give someone an errand"
nadar          nadaar           # "swim"
protestar      protestaar       # "protest"
sospechar      sohspechaar      # "suspect"
tomar\ fot     tomaar\ foʼt     # "take a picture (of)"
uas            uaʼs             # "use"
xgab:xgab      x꞉gaʼab:x꞉gaʼab  # "think"
xgab:xjab      x꞉gaʼab:x꞉jaʼab  # "think"    ! Dir/LR
xten           x꞉tèeʼn          # "work as"
zeiny          zèèiʼny          # "work"


LEXICON V-Stems-Irreg(3)

uny[-perf]:uny[-perf]            uhny[-perf]:uny[-perf]             <tv> # "do"
uny[p1pl]:ieny[p1pl]             uhny[p1pl]:ieʼny[p1pl]             <tv> # "do"
uny[-p1pl,perf]:einy[-p1pl,perf] uhny[-p1pl,perf]:èiʼny[-p1pl,perf] <tv> # "do"


LEXICON V-Stems(3)

ban               bàaʼan                   <iv>    # "follow a medical diet"
beb               bèeʼb                    <tv>    # "ride (an animal)"  # FIXME: irreg irrealis
bix               biʼihx                   <iv>    # "ring (of a bell)"
chiby             chìiby                   <tv>    # "scare"
culozac           culohzaʼc                <tv>    # "take good care of"
cuzh              cuhzh                    <tv>    # "play (a wind instrument)"
cwany             cwàaʼnny                 <tv>    # "wake up"
cwez              cwèez                    <tv>    # "turn off"
cwual             cwùaʼll                  <tv>    # "turn on"
cyetlaz           cyeʼtlààaʼz              <iv>    # "be/get happy"
deidy:{D}eidy     dèèiʼdy:(D)èèiʼdy        <tv>    # "cross"  # FIXME: irreg irrealis
deidy:{l}deidy    dèèiʼdy:{l}dèèiʼdy       <dtv>   # "give"    # {l}:l in <prog>
deidy:{n}deidy    dèèiʼdy:{n}dèèiʼdy       <dtv>   # "give"    # {n}:n in <prog>  ! Dir/LR
dieby             dììeʼby                  <iv>    # "boil"
dily:{D}ily       dìiʼlly:{D}ìiʼlly        <tv>    # "fight with, scold"  # FIXME: irreg irrealis
dinal             dinàall                  <tv>    # "chase"
duax              duʼahx                   <iv>    # "bark"
dub:{l}dub        dùuʼb:{l}dùuʼb           <iv>    # "sweep"
dub:{n}dub        dùuʼb:{n}dùuʼb           <iv>    # "sweep"     ! Dir/LR
gap:{G}ap         gàaʼp:{G}àaʼp            <tv>    # "slap"
gats              gàaʼts                   <iv>    # "get buried"
guad              gùad                     <tv>    # "sting"
gub:{G}ub         gùùuʼb:{G}ùùuʼb          <tv>    # "suck; smoke; use a lot (of fuel)"
guch              guhch                    <tv>    # "bathe"
guez:{G}{u}ez     guèeʼez:{G}{u}èeʼez      <tv>    # "hug; carry (in arms)"
guiby:{G}{u}iby   guìiʼby:{G}{u}ìiʼby      <tv>    # "wash"
guieb:{G}{u}ieb   guìeb:{G}{u}ìeb          <tv>    # "sew"
guily:{G}{u}ily   guìiʼlly:{G}{u}ìiʼilly   <tv>    # "look for"  ! Dir/LR
guily:{G}{u}ily   guìiʼlly:{G}{u}ìiʼlly    <tv>    # "look for"
guiny:{G}{u}iny   guìiʼiny:{G}{u}ìiʼiny    <tv>    # "borrow; ask for hand (of girl) in marriage (in betrothal negotiation)"
guix:{G}{u}ix     guìiʼx:{G}{u}ìiʼx        <tv>    # "lay down, put down (long object)"
guixicy:{G}{u}ixicy  guixiʼihcy:{G}{u}ixiʼihcy  <iv>   # "nod off (to sleep)"
guizh:{G}{u}izh   guiʼihzh:{G}{u}iʼihzh    <dtv>   # "pay"
gwezac            gwèeʼzaʼc                <tv>    # "speak (a language) well"
gyan              gyaàaʼn                  <dtv>   # "feed"
nab               nààaʼb                   <tv>    # "ask/send for"
nalaz             nnahlààaʼz               <tv>    # "remember, miss (a person), think about"
naz               nnàaʼaz                  <tv>    # "grab"
ni                nììiʼ                    <tv>    # "say"
nudizh            nuhdìiʼzh                <tv>    # "ask of"
nyity             nyiʼihty                 <iv>    # "get lost, be missing"
nyity             nyiʼihty                 <tv>    # "lose, miss (a thing)"
reizh             rèeiʼizh                 <tv>    # "measure"
saguel            saguehll                 <iv>    # "be so good as to"
san               sàaʼan                   <tv>    # "leave behind"
seidy             sèèiʼdy                  <dtv>   # "teach"
seidy             sèèiʼdy                  <tv>    # "learn"
studian           studiʼaann               <tv>    # "student"
studian:estudian  studiʼaann:estudiʼaann   <tv>    # "student"
subiaz            subihahz                 <tv>    # "dry"
sudieby           sudììeʼby                <tv>    # "boil"
taz               tàaʼaz                   <tv>    # "hit"
teidy             tèèiʼdy                  <tv>    # "take/bring across"
tyepy             tyèeʼpy                  <iv>    # "whistle"
tyis              tyiʼihs                  <iv>    # "jump"
xyeily            xyeʼilly                 <tv>    # "open"
yac               yaʼahc                   <iv>    # "heal, get better/well"
yulaz             yuʼlààaʼz                <tv>    # "like"  # FIXME: not used with <perf>
zhiby             zhihby                   <iv>    # "get scared"
zhiez             rzhìez                   <iv>    # "laugh, smile"
zhuny             zh꞉ùuʼnny                <iv>    # "run; elope"
zhuny:zhyuny      zh꞉ùuʼnny:zhy꞉ùuʼnny     <iv>    # "run; elope"   # ! Dir/LR
zhyag             zhyàag                   <tv>    # "meet"
zhyeil            zhyeʼilly                <iv>    # "open"
zilaz             zilààaʼz                 <tv>    # "used to think that"


LEXICON N-Stems-Possessed(2)

bzyan            bzyaàaʼn             # "brother (of opposite sex); sister (of opposite sex)"


LEXICON N-Stems(2)

Dizhsa           Dìiʼzhsah            # "Zapotec language"
Dizhtily         Dìiʼzhtiilly         # "Spanish language"
Dyoz             Dyooz                # "G-d"
Ingles           Ingleʼs              # "English language"
Lia              Lia                  # person word for females
Tiu              Tiʼu                 # person word for males (Señor, Don)
amiegw           amieʼgw              # "friend"
apartamen        apartameʼenn         # "apartment"
autobuas         autobuaʼs            # "bus"
bag              baag                 # "cow"
banua            bannùùaʼ             # "scorpion"
bar              baar                 # "stick, pole"
basquetbol       basqueʼtboʼll        # "basketball"
bca              bcaʼah               # "crow"
bdo              bdòoʼ                # "baby"
bdua             bdùùaʼ               # "banana"
becw             bèeʼcw               # "dog"
beld:bel         behlld:behll         # "fish"               
beld:bel         bèèeʼlld:bèèeʼll     # "snake"
beld:beld        behlld:behlld        # "fish"         !  Dir/LR
beld:beld        bèèeʼlld:bèèeʼlld    # "snake"        !  Dir/LR
bistied          bistiied             # "dress"
blal             blàaal               # "blal (precolumbian earthenware figure)"
bolz             boʼoollz             # "bag"
budy             buʼuhuhdy            # "chicken"
budy\ gwuar      buʼuhdy\ gwuʼuar     # "turkey"
budy\ ngual      buʼuhdy\ nguʼahll    # "male turkey"
buny             bùunny               # "person, man"
bxady            bx꞉àady              # "chapulin"
bzeiny           bzèiny               # "deer"
bzeiny:bzëiny    bzèiny:bzêiny        # "deer"  ! Dir/LR
cabai            cabaʼi               # "horse"
caj              caʼj                 # "box"
campan           campaʼnn             # "bell"
camyuny          cammyunny            # "bus; truck; car; van"
carr             caʼrr                # "car; bus; truck"
cart             caʼrt                # "letter"
chirmia          chirmia              # "traditional flute"
clarinet         clarineʼt            # "clarinet"
coch             coʼch                # "car"
computador       computadoor          # "computer"
cos              coʼs                 # "thing"
cotony           cotoony              # "shirt/blouse"
coyot            coyoʼt               # "‘coyote’ - cross-border escort"
cub              cuʼuhb               # "tejate, masa"
cwen             cweʼenn              # "story"
doctor           doʼctoor             # "doctor"
dolr             doʼlr                # "dollar"
edifisy          edifiiʼsy            # "building"
fald             faʼalld              # "skirt"
fot              foʼt                 # "photograph"
gal\ rguily\ dizh  gahll\ rguìiʼlly\ dìiʼzh  # "word search"
gasolyen         gasolyeʼnn           # "gas, gasoline"
gaxlyu           gahxlyuh             # "world"
grabador         grabadoor            # "tape recorder"
guan             gùuʼann              # "bull"
guet             gueht                # "tortilla"
guet\ xtily      gueht\ xtiilly       # "bread"
guez             guehehz              # "cigarette"
gyag             gyahg                # "stick, tree, wood, firewood"
lapy             laʼpy                # "pencil"
lecsyony         lecsyoony            # "lesson"
liebr            liʼebr               # "book"
liny             liʼnny               # "border (between two countries)"
mansan           mannsaan             # "apple"
many             maʼany               # "animal"
manyser          maʼanyseer           # "bee"
mes              meʼs                 # "teacher"
meser            meseer               # "waiter, waitress"
milagr           milàagr              # "miracle"
mna              mnààaʼ               # "woman"
mniny            mnìiʼiny             # "child, kid, boy"
muly             mùuully              # "money"
myes             myeʼes               # "mass (at a church)"
nyis             nyiʼihs              # "water"
pasaprt          pasapoʼrt            # "passport"
pelot            peloʼt               # "ball"
pes              peʼs                 # "peso"
plum             pluʼmm               # "pen"
polisia          polisiia             # "policeman; police"
pwert            pweeʼrt              # "door"
pyep             pyeʼp                # "pipe; tanker truck"
rmudy            rmuudy               # "medicine"
rrady            rraady               # "radio"
rregal           rregaʼll             # "gift"
rresieb          rresiieb             # "bill (for service); receipt"
sede             sedee                # "CD"
tarea            tareea               # "exercise, homework"
telebisyony      telebisyoony         # "television"
tiu              tiʼu                 # "uncle"
unida            unidaa               # "unit"
wbwan            wbwààaʼn             # "theif"
wi               wii                  # "guava"
ydo              ydòòoʼ               # "church"
yu               yuʼuh                # "house"
zhi              zh꞉ih                # "day"
zhieb            zhiʼeb               # "goat"
zhily            zhiʼìilly            # "sheep"
zhomrel          zh꞉ommreel           # "hat"
zhyet            zhyeʼet              # "cat"


LEXICON NP-Stems(3)

Bac       Baʼahc         <top>       # "Tlacolula (de Matamoros)"
Bed       Beed           <ant><m>    # "Pedro"
Glory     Gloory         <ant><f>    # "Gloria"
Gyeily    Gyeʼeihlly     <ant><m>    # "Miguel"
Jwany     Jwaany         <ant><m>    # "Juan"
Len       Leen           <ant><f>    # "Elena"
Mazh      Maʼazh         <ant><m>    # "Tomás/Thomas"
Nach      Naʼch          <ant><m>    # "Ignacio, Nacho, Ignatius"
Petr      Peʼtr          <ant><f>    # "Petra"
Rony      Roʼony         <ant><m>    # "Jeronimo"
San\ Luc  Sann\ Luʼuc    <top>       # "San Lucas Quiaviní"
Tyen      Tyeʼnn         <ant><f>    # "Cristina, Christina"
Xandan    Xanndaan       <top>       # "Santa Ana del Valle"
Xgyia     X꞉gyìiʼah      <top>       # "Teotitlán del Valle"


LEXICON Prep-Stems(2)

cuan      cuahnn      # "and, with"
quën      quëhnn      # "and, with"
xte       x꞉tèeʼ      # "of"
xten      x꞉tèeʼn     # "of"    # Dir/LR    # FIXME
zalo      zalloh      # "about" (?)

LEXICON Det-Stems(3)

cha       chah     <cls>   # "one, a/an (with breadstuffs)"
ra        ra       <qnt>   # (pl)
rata      raʼtaʼ   <qnt>   # "every"
teiby     teiby    <idf>   # "a/an"

LEXICON Postdet-Stems(3)

re        rèe      <dem>   # "that"
re        rèeʼ     <dem>   # "this"

LEXICON Adj-Stems(2)

cweby            cweby               # "new"

LEXICON Adv-Stems(2)

a                a                   # "already" (completion of verb)
na               nah                 # "now"
nai              nàiʼ                # "yesterday"
queity:queity    queʼity:queiʼty     # "not"
queity:quëity    queʼity:quëiʼty     # "not"   ! Dir/LR
uas              uʼas                # "really"
zhi:zhi          zhii:zhii           # "tomorrow"
zhi:yzhi         zhii:yzhii          # "tomorrow"  ! Dir/LR

LEXICON Adv-Stems-Itg(2)

bal              bàall               # "how many"
bal:bald         bàall:bàalld        # "how many"  ! Dir/LR
blac             blaʼac              # "how much (money)"
guc:guc          guʼc:guʼc           # "when"
guc:uc           guʼc:uʼc            # "when"      ! Dir/LR
xa               x꞉a                 # "how"
xa\ mod          x꞉a\ moʼod          # "how"
xi\ ni           xi\ niʼih           # "why"


LEXICON Number-Stems(2)

teiby            teʼihby             # "one"
tyop             tyoʼp               # "two"
chon             chòonn              # "three"
tap              tahp                # "four"
gai              gàiʼ                # "five"
xop              x꞉òp                # "six"
gaz              gàaz                # "seven"
xon              x꞉òon               # "eight"
ga               gààaʼ               # "nine"
tsë              tsêëʼ               # "ten"
tsëbteby         tsëbteeby           # "eleven"
tsëbtyop         tsëbtyoʼp           # "twelve"
tseiny           tsèeʼiny            # "thirteen"
tsëda            tsëdah              # "fourteen"
tseiny           tsèèiʼny            # "fifteen"
tseinyabteby     tsèiʼnyahbteeby     # "sixteen"
tseinyabtyop     tsèiʼnyahbtyoʼp     # "seventeen"
tseinyabchon     tsèiʼnyahbchòonn    # "eighteen"
tseinyabtap      tsèiʼnyahbtaʼp      # "nineteen"
galy:galy        gàally:gàally       # "twenty"
galy:galyd       gàally:gàallyd      # "twenty"     ! Dir/LR


LEXICON Ij-Stems(2)

a                àaaʼ                # "yes"
as               aʼs                 # "hi, hey"
as\ nia          aʼs\ nniìaʼ         # "hey there"
queity:queity    queʼity:queiʼty     # "no"
queity:quëity    queʼity:quëiʼty     # "no"   ! Dir/LR


LEXICON Mod-Stems(2)    # modal particles

e         èee           # yes/no question marker
na        nah           # optionally used at end of wh questions



LEXICON Punctuation

.<sent>:.
..<sent>:..
...<sent>:...
;<sent>:;
\:<sent>:\:
!<sent>:!
-<guio>:-
—<guio>:—
,<cm>:,
?<sent>:?
'<apos>:'
"<sent>:"
«<lquot>:«
»<rquot>:»
”<rquot>:”
“<lquot>:“
(<lpar>:(
\]<rpar>:\]
\[<lpar>:\[
)<rpar>:)
\\<sent>:\\
/<sent>:/


