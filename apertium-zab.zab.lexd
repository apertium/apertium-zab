#!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
#!!                                                                         !!!
#!!            V A L L E Y  Z A P O T E C  T R A N S D U C E R              !!!
#!!                                                                         !!!
#!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

#=============================================================================!
#                         Verb Morphotactics                                  !
#=============================================================================!

PATTERNS

# :Tense :Caus V-Stems-TV(1) v-tag: V-Stems-TV(3): Caus: Tense:
:Tense V-Stems-TV(1) :V-Extender(1) :V-Neg(1) :V-Subj(1) v-tag: V-Stems-TV(3): V-Extender(1): Tense: V-Neg(1): V-Subj(1):

:Tense V-Stems-IV(1) :V-Extender(1) :V-Neg(1) :V-Subj(1) v-tag: V-Stems-IV(3): V-Extender(1): Tense: V-Neg(1): V-Subj(1):

:Tense V-Stems-TV(1): :V-Stems-TV(2) :V-Extender(2) :V-Neg(2) :V-Subj(2) v-tag: V-Stems-TV(3): V-Extender(2): Tense: V-Neg(2): V-Subj(2):  # ! Dir/LR

:Tense V-Stems-IV(1): :V-Stems-IV(2) :V-Extender(2) :V-Neg(2) :V-Subj(2) v-tag: V-Stems-IV(3): V-Extender(2): Tense: V-Neg(2): V-Subj(2):  # ! Dir/LR

# new orthography analysis
N-Stems(1) n-tag:
N-Stems(1): :N-Stems(2) n-tag:    # Dir/LR

:N-Poss(1) N-Stems(1) n-tag: N-Poss(3): V-Subj(1)
:N-Poss(2) N-Stems(1): :N-Stems(2) n-tag: N-Poss(3): V-Subj(1)  # Dir/LR

N-Stems-Possessed(1) n-tag: V-Subj(1)
N-Stems-Possessed(1): :N-Stems-Possessed(2) n-tag: :V-Subj(2)   # Dir/LR


## old orthography analysis
#N-Stems(2) n-tag:

NP-Stems(1) np-tag: NP-Stems(3):
NP-Stems(1): :NP-Stems(2) np-tag: NP-Stems(3):   # ! Dir/LR

Prep-Stems(1) pr-tag:
Prep-Stems(1): :Prep-Stems(2) pr-tag:  # ! Dir/LR

Det-Stems(1) det-tag: Det-Stems(3):
Det-Stems(1): :Det-Stems(2) det-tag: Det-Stems(3):   # ! Dir/LR

Postdet-Stems(1) postdet-tag: Postdet-Stems(3):
Postdet-Stems(1): :Postdet-Stems(2) postdet-tag: Postdet-Stems(3):  # ! Dir/LR

Adv-Stems(1) adv-tag:
Adv-Stems(1): :Adv-Stems(2) adv-tag:  # ! Dir/LR

Adj-Stems(1) adj-tag:
Adj-Stems(1): :Adj-Stems(2) adj-tag:  # ! Dir/LR

Ij-Stems(1) ij-tag:
Ij-Stems(1): :Ij-Stems(2) ij-tag:    # ! Dir/LR

Mod-Stems(1) mod-tag:
Mod-Stems(1): :Mod-Stems(2) mod-tag:  # ! Dir/LR

Punctuation


LEXICON Tense

<hab>:r>        # habitual
<perf>:{B}>     # perfective
<prog>:ca>      # progressive

LEXICON N-Poss(3)

# FIXME: deal with irregulars, e.g. xyecw/x꞉yèeʼcw for becw
x     x꞉     <px>    # for adding pronominal possession to Ns FIXME: CHECK(2)



LEXICON V-Subj(2)           # FIXME

<p1><sg>:>a                 <p1><sg>:>aʼ
<p1><pl>:>{u}ën             <p1><pl>:>{u}ëhnn

<p2><infm><sg>:>u           <p2><infm><sg>:>ùuʼ
<p2><frm><sg>:>yu           <p2><frm><sg>:>yuuʼ
<p2><frm><sg>:>yëbu         <p2><frm><sg>:>yëbuuʼ   # ! Dir/LR
<p2><infm><pl>:>ad          <p2><infm><pl>:>ad      # FIXME: CHECK(2)
<p2><frm><pl>:>yuad         <p2><frm><sg>:>yùad
<p2><frm><pl>:>yëbuad       <p2><frm><sg>:>yëbùad   # ! Dir/LR

<p3><prox><sg>:>{u}ëng      <p3><prox><sg>:>{u}ëng
<p3><dist><sg>:>{u}i        <p3><dist><sg>:>{u}ih
<p3><resp><sg>:>{u}ëb       <p3><resp><sg>:>{u}ëhb  # FIXME: CHECK(2)
<p3><anml><sg>:>{u}ëm       <p3><anml><sg>:>{u}ëhm  # FIXME: CHECK(2)
<p3><fam><sg>:>azh          <p3><fam><sg>:>azh      # FIXME: CHECK(2)
<p3><rev><sg>:>iny          <p3><rev><sg>:>iny      # FIXME: CHECK(2), (1): iny/ni

<p3><prox><pl>:>rëng        <p3><prox><pl>:>rëng    # FIXME: CHECK(2)
<p3><dist><pl>:>ri          <p3><dist><pl>:>rih     # FIXME: CHECK(2)
<p3><resp><pl>:>rëb         <p3><resp><pl>:>rëhb    # FIXME: CHECK(2)
<p3><anml><pl>:>rëm         <p3><anml><pl>:>rëhm    # FIXME: CHECK(2)
<p3><fam><pl>:>razh         <p3><fam><pl>:>razh     # FIXME: CHECK(2)
<p3><rev><pl>:>riny         <p3><rev><pl>:>riny     # FIXME: CHECK(2)

:                           :

LEXICON V-Neg(2)            # FIXME        

<neg>:>d{I}                 <neg>:>d{I}ʼ
:                           :

LEXICON V-Extender(2)       # FIXME
<pol>:>la                   <pol>:>làaʼ
:                           :

#LEXICON Caus
#
#<caus>:su
#:


LEXICON v-tag

<v>

LEXICON n-tag

<n>

LEXICON pr-tag

<pr>

LEXICON np-tag

<np>

LEXICON det-tag

<det>

LEXICON postdet-tag

<postdet>

LEXICON adv-tag

<adv>

LEXICON adj-tag

<adj>

LEXICON ij-tag

<ij>

LEXICON mod-tag

<mod>


LEXICON V-Stems-IV(3)

ban       bàaʼan      <iv>    # "follow a medical diet"
bix       biʼihx      <iv>    # "ring (of a bell)"
cyetlaz   cyeʼtlààaʼz <iv>    # "be/get happy"
dieby     dììeʼby     <iv>    # "boil"
duax      duʼahx      <iv>    # "bark"
nyity     nyiʼihty    <iv>    # "get lost, be missing"
tyepy     tyèeʼpy     <iv>    # "whistle"
tyis      tyiʼihs     <iv>    # "jump"
yac       yaʼahc      <iv>    # "heal, get better/well"
zhiby     zhihby      <iv>    # "get scared"
zhiez     rzhìez      <iv>    # "laugh, smile"
zhuny     zh꞉ùuʼnny   <iv>    # "run; elope"
zhyeil    zhyeʼilly   <iv>    # "open"


LEXICON V-Stems-TV(3)

beb             bèeʼb               <tv>    # "ride (an animal)"
chiby           chìiby              <tv>    # "scare"
cuzh            cuhzh               <tv>    # "play (a wind instrument)"
cwany           cwàaʼnny            <tv>    # "wake up"
cwez            cwèez               <tv>    # "turn off"
cwual           cwùaʼll             <tv>    # "turn on"
deidy:{l}deidy  dèèiʼdy:{l}dèèiʼdy  <dtv>   # "give"    # {l}:l in <prog>
deidy:{n}deidy  dèèiʼdy:{n}dèèiʼdy  <dtv>   # "give"    # {n}:n in <prog>
guad            gùad                <tv>    # "sting"
guch            guhch               <tv>    # "bathe"
gwezac          gwèeʼzaʼc           <tv>    # "speak (a language) well"
gyan            gyaàaʼn             <dtv>   # "feed"
nab             nààaʼb              <tv>    # "ask/send for"
nalaz           nnahlààaʼz          <tv>    # "remember, miss (a person), think about"
naz             nnàaʼaz             <tv>    # "grab"
ni              nììiʼ               <tv>    # "say"
nudizh          nuhdìiʼzh           <tv>    # "ask of"
nyity           nyiʼihty            <tv>    # "lose, miss (a thing)"
san             sàaʼan              <tv>    # "leave behind"
seidy           sèèiʼdy             <dtv>   # "teach"
seidy           sèèiʼdy             <tv>    # "learn"
subiaz          subihahz            <tv>    # "dry"
sudieby         sudììeʼby           <tv>    # "boil"
taz             tàaʼaz              <tv>    # "hit"
xyeily          xyeʼilly            <tv>    # "open"
yulaz           yuʼlààaʼz           <tv>    # "like"  # FIXME: not used with <perf>


LEXICON N-Stems-Possessed(2)

bzyan            bzyaàaʼn             # "brother (of opposite sex); sister (of opposite sex)"


LEXICON N-Stems(2)

Dizhsa           Dìiʼzhsah            # "Zapotec language"
Dizhtily         Dìiʼzhtiilly         # "Spanish language"
Ingles           Ingleʼs              # "English language"
Lia              Lia                  # person word for females
Tiu              Tiʼu                 # person word for males (Señor, Don)
amiegw           amieʼgw              # "friend"
apartamen        apartameʼenn         # "apartment"
bag              baag                 # "cow"
banua            bannùùaʼ             # "scorpion"
bar              baar                 # "stick, pole"
basquetbol       basqueʼtboʼll        # "basketball"
bdo              bdòoʼ                # "baby"
bdua             bdùùaʼ               # "banana"
becw             bèeʼcw               # "dog"
beld:bel         behlld:behll         # "fish"               
beld:beld        behlld:behlld        # "fish"         !  Dir/LR
blal             blàaal               # "blal (precolumbian earthenware figure)"
bolz             boʼoollz             # "bag"
budy             buʼuhuhdy            # "chicken"
budy\ gwuar      buʼuhdy\ gwuʼuar     # "turkey"
budy\ ngual      buʼuhdy\ nguʼahll    # "male turkey"
buny             bùunny               # "person, man"
bxady            bx꞉àady              # "chapulin"
cabai            cabaʼi               # "horse"
campan           campaʼnn             # "bell"
chirmia          chirmia              # "traditional flute"
clarinet         clarineʼt            # "clarinet"
cos              coʼs                 # "thing"
cwen             cweʼenn              # "story"
doctor           doʼctoor             # "doctor"
fot              foʼt                 # "photograph"
grabador         grabadoor            # "tape recorder"
guan             gùuʼann              # "bull"
guet             gueht                # "tortilla"
guet\ xtily      gueht\ xtiilly       # "bread"
gyag             gyahg                # "stick, tree, wood, firewood"
lapy             laʼpy                # "pencil"
lecsyony         lecsyoony            # "lesson"
liebr            liʼebr               # "book"
mansan           mannsaan             # "apple"
manyser          maʼanyseer           # "bee"
mes              meʼs                 # "teacher"
mna              mnààaʼ               # "woman"
mniny            mnìiʼiny             # "child, kid, boy"
muly             mùuully              # "money"
nyis             nyiʼihs              # "water"
plum             pluʼmm               # "pen"
pwert            pweeʼrt              # "door"
rrady            rraady               # "radio"
sede             sedee                # "CD"
tarea            tareea               # "exercise, homework"
telebisyony      telebisyoony         # "television"
tiu              tiʼu                 # "uncle"
unida            unidaa               # "unit"
wbwan            wbwààaʼn             # "theif"
wi               wii                  # "guava"
zhi              zh꞉ih                # "day"
zhieb            zhiʼeb               # "goat"
zhily            zhiʼìilly            # "sheep"
zhomrel          zh꞉ommreel           # "hat"
zhyet            zhyeʼet              # "cat"



LEXICON NP-Stems(3)

Bac       Baʼahc         <top>       # "Tlacolula (de Matamoros)"
Bed       Beed           <ant><m>    # "Pedro"
Glory     Gloory         <ant><f>    # "Gloria"
Gyeily    Gyeʼeihlly     <ant><m>    # "Miguel"
Jwany     Jwaany         <ant><m>    # "Juan"
Len       Leen           <ant><f>    # "Elena"
Mazh      Maʼazh         <ant><m>    # "Tomás/Thomas"
Petr      Peʼtr          <ant><f>    # "Petra"
Rony      Roʼony         <ant><m>    # "Jeronimo"
San\ Luc  Sann\ Luʼuc    <top>       # "San Lucas Quiaviní"
Xandan    Xanndaan       <top>       # "Santa Ana del Valle"
Xgyia     X꞉gyìiʼah      <top>       # "Teotitlán del Valle"


LEXICON Prep-Stems(2)

cuan      cuahnn      # "and, with"
quën      quëhnn      # "and, with"
xte       x꞉tèeʼ      # "of"
xten      x꞉tèeʼn     # "of"    # Dir/LR    # FIXME
zalo      zalloh      # "about" (?)

LEXICON Prn-Stems(3)

tu           tu               <itg>   # "who"
xi           xi               <itg>   # "what"
lai          laʼàiʼ           <pers><p3><dist><sg>  # "he/she/it (distal)"
laëng        laʼa-ëng         <pers><p3><prox><sg>  # "he/she/it (proximal)"
laëb         làaʼ-ëhb         <pers><p3><resp><sg>  # "he/she/it (respectful)"
liu          lìuʼ             <pers><p2><infm><sg>  # "you (sg, informal)"
laad         làaʼ-ahd         <pers><p2><infm><pl>  # "you (pl, informal)"
yu           yuʼuuʼ           <pers><p2><frm><sg>   # "you (sg, formal)"
yu:yëbu      yuʼuuʼ:yəbuʼuuʼ  <pers><p2><frm><sg>   # "you (sg, formal)" ! Dir/LR
yuad         yùad             <pers><p2><frm><pl>   # "you (pl, formal)"
yuad:yëbuad  yuad:yëbùad      <pers><p2><frm><pl>   # "you (pl, formal)" ! Dir/LR
danoën:danoën dannooh-ëhnn:dannooh-ëhnn  <pers><p1><pl> # "we"
danoën:danuën dannooh-ëhnn:dannuuh-ëhnn  <pers><p1><pl> # "we"  ! Dir/LR

LEXICON Det-Stems(3)

cha       chah     <cls>   # "one, a/an (with breadstuffs)"
ra        ra       <qnt>   # (pl)
rata      raʼtaʼ   <qnt>   # "every"
teiby     teiby    <idf>   # "a/an"

LEXICON Postdet-Stems(3)

re        rèe      <dem>   # "that"
re        rèeʼ     <dem>   # "this"

LEXICON Adj-Stems(2)

cweby            cweby               # "new"

LEXICON Adv-Stems(2)

a                a                   # "already" (completion of verb)
na               nah                 # "now"
nai              nàiʼ                # "yesterday"
queity:queity    queʼity:queiʼty     # "not"
queity:quëity    queʼity:quëiʼty     # "not"   ! Dir/LR
uas              uʼas                # "really"


LEXICON Ij-Stems(2)

a                àaaʼ                # "yes"
queity:queity    queʼity:queiʼty     # "no"
queity:quëity    queʼity:quëiʼty     # "no"   ! Dir/LR


LEXICON Mod-Stems(2)    # modal particles

e         èee           # yes/no question marker



LEXICON Punctuation

.<sent>:.
..<sent>:..
...<sent>:...
;<sent>:;
\:<sent>:\:
!<sent>:!
-<guio>:-
—<guio>:—
,<cm>:,
?<sent>:?
'<apos>:'
"<sent>:"
«<lquot>:«
»<rquot>:»
”<rquot>:”
“<lquot>:“
(<lpar>:(
]<rpar>:]
[<lpar>:[
)<rpar>:)
\\<sent>:\\
/<sent>:/


