Alphabet
 A B C D E Ë F G H I J K L M N O P Q R S T U V W X Y Z
 a b c d e ë f g h i j k l m n o p q r s t u v w x y z
 À È Ê Ì Ò Ù
 à è ê ì ò ù ꞉ ʼ
 %{B%}:b %{B%}:m %{B%}:w
 %{I%}:i %{I%}:y
 %{l%}:0 %{l%}:l
 %{n%}:0 %{n%}:n
 %{u%}:0 %{u%}:u
 %{Y%}:y %{Y%}:g
 %{G%}:g %{G%}:c %{G%}:q %{G%}:d
 %{C%}:c %{C%}:q
 %{y%}:0 %{y%}:y
 %{g%}:0 %{g%}:g
 %>:0  ;

Sets

Nasal = N M 
        n m ;

quVowels = I E Ë Ì
           i e ë ì ;

KV = À È Ê Ì Ò Ù
     à è ê ì ò ù ;

Vow = A E Ë I O U
      a e ë i o u ;

Cons = B C D F G H J K L M N P Q R S T V W X Y Z 
       b c d f g h j k l m n p q r s t v w x y z ꞉ ;

Rules 

"{B} as m before n"
%{B%}:m <=> _ %>:* :n ;

"{B} as w before b"
%{B%}:w <=> _ %>:* :b ;

"{Y} as g before y"
%{Y%}:g <=> _ %>:* :y ;

"disappearing consonants only surface in progressive"
Cx:Cy <=> c a %>:* _ ;
      where Cx in ( %{l%} %{n%} )
            Cy in (   l     n   )
      matched ;

"c{u} as qu if before i/e/ë: {u}:u"
%{u%}:u <=> [ c: | %{C%}: ] %>:* _ :quVowels ;
            [ :g | :q ] _ :quVowels ;

"c{u} as qu if before i/e/ë: c:q"
c:q <=> _ %>:* %{u%}:u ;

"KKC → KC in combined form"
KV:0 <=> :KV _ :Vow :ʼ :Cons* %>: ;   ! FIXME: doesn't seem to work??
     where KV in KV ;

!@ r >:0 s è è:0 i ʼ d y >:0 {u}:0 i h

"BB → PB in combined form"
h:0 <=> :Vow _ :Vow :h :Cons* %>: ;

!@ r >:0 s u b i h:0 a h z > {u}:0 i h

! FIXME: w>bahnny>{u}ëng:wbàaʼnyëng

"diʼ → dy before vowels: {I}"
%{I%}:y <=> _ (ʼ:) %>:* :0* :Vow ;

! FIXME: does ʼ delete?
"diʼ → dy before vowels: ʼ"
ʼ:0 <=> %{I%}:y _ ;

"{G} devoices after {Y} → c (irre)"
%{G%}:c <=> %{Y%}: %>: _ \%{u%}: ;

"{G} devoices after {Y} → q (irre)"
%{G%}:q <=> %{Y%}: %>: _ %{u%}: ;

"{G} is d after {B} (perf)"
%{G%}:d <=> %{B%}: %>: _ ;

"{D} devoices after {Y} (irre)"
%{D%}:t <=> %{Y%}: %>: _ ;

"{Y} deletes before {G} and {D} but not between g and i"
%{Y%}:0 <=> _ %>: [ %{G%}: | %{D%}: ] ;
            %{g%}:g _ %>: [ :Vow - :i ] ;  ! FIXME: tentative pattern - check

"{y} is y only before a vowel"
%{y%}:y <=> _ [ %>: :Vow ]/[ :0 - %>: ]* ;

"{g} is g only if a vowel follows"
%{g%}:g <=> _ %{Y%}: %>: :Vow ;

!"{G} is g unless after {Y} or {B}"
!{G}:g <=> _ ;
!      except
!         {Y}

"{C} is q only before {u}:u; otherwise c"
%{C%}:q <=> _ ( %>: ) %{u%}:u ;

"Delete possessive prefix before x (x:0)"
x:0 <=> _ ( ꞉: ) %>: x: ;

"Delete possessive prefix before x (꞉:0)"
꞉:0 <=> x: _ %>: x: ;



! Resources:
! http://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Enter_twol
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! https://github.com/hfst/
! http://wiki.apertium.org/wiki/Twol
! http://wiki.apertium.org/wiki/Hfst
